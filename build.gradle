plugins {
    id "java"
    id "maven-publish"
    id "checkstyle"
    id "jacoco"
    id "org.embulk.embulk-plugins" version "0.4.0"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "io.github.sonots"
version = "0.7.1"
description = "A filter plugin for Embulk to filter out columns."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    compile("io.github.medjed:JsonPathCompiler:0.0.12") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.apache.commons", module: "commons-lang3"
    }

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.9.23"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-standards:0.9.23"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
}

embulkPlugin {
    mainClass = "org.embulk.filter.column.ColumnFilterPlugin"
    category = "filter"
    type = "column"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        // TODO: Configure some public Maven repo(s).
    }
}

gem {
    authors = [ "Naotoshi Seo" ]
    email = [ "sonots@gmail.com" ]
    summary = "A filter plugin for Embulk to filter out columns"
    homepage = "https://github.com/sonots/embulk-filter-column"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

checkstyle {
    toolVersion = '6.7'
}
