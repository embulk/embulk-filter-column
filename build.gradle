plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "checkstyle"
    id "jacoco"
}

repositories {
    mavenCentral()
    // JsonPathCompiler only available in JCenter.
    jcenter()
}

group = "org.embulk"
version = "0.9.0-SNAPSHOT"
description = "A filter plugin for Embulk to filter out columns."

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation "org.embulk:embulk-util-config:0.3.4"

    implementation("io.github.medjed:JsonPathCompiler:0.1.2") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.apache.commons", module: "commons-lang3"
    }
    implementation "org.embulk:embulk-util-timestamp:0.2.2"
    implementation "org.embulk:embulk-util-json:0.2.2"

    implementation "org.apache.commons:commons-lang3:3.4"

    testCompileOnly "org.embulk:embulk-spi:0.11"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.embulk:embulk-core:0.11.0"
    testImplementation "org.embulk:embulk-deps:0.11.0"
    testImplementation "org.embulk:embulk-junit4:0.11.0"
    testImplementation "com.google.guava:guava:18.0"
}

embulkPlugin {
    mainClass = "org.embulk.filter.column.ColumnFilterPlugin"
    category = "filter"
    type = "column"
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
        from rootProject.file("NOTICE")
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
        from rootProject.file("NOTICE")
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
        from rootProject.file("NOTICE")
    }
}

// It should not publish a `.module` file in Maven Central.
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
         maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

             from components.java
             // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
             // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        name = "Naotoshi Seo"
                        email = "sonots@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-filter-column.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-filter-column.git"
                    url = "https://github.com/embulk/embulk-filter-column"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        logger.lifecycle("Signing with an in-memory key.")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.maven
}

gem {
    authors = [ "Naotoshi Seo" ]
    email = [ "sonots@gmail.com" ]
    summary = "A filter plugin for Embulk to filter out columns"
    homepage = "https://github.com/embulk/embulk-filter-column"
    licenses = [ "Apache-2.0" ]

    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE_GEM")
    rename ("NOTICE_GEM", "NOTICE")
}

gemPush {
    host = "https://rubygems.org"
}

checkstyle {
    toolVersion = '6.7'
}
